locals {
  name = var.project
}

resource "aws_vpc" "main" {
  cidr_block           = "10.0.0.0/16"
  enable_dns_support   = true
  enable_dns_hostnames = true
  tags = { Name = "${local.name}-vpc" }
}

resource "aws_subnet" "public_a" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = "10.0.1.0/24"
  availability_zone       = "${var.aws_region}a"
  map_public_ip_on_launch = true
  tags = { Name = "${local.name}-public-a" }
}

resource "aws_subnet" "public_b" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = "10.0.2.0/24"
  availability_zone       = "${var.aws_region}b"
  map_public_ip_on_launch = true
  tags = { Name = "${local.name}-public-b" }
}

resource "aws_internet_gateway" "igw" {
  vpc_id = aws_vpc.main.id
}

resource "aws_route_table" "public" {
  vpc_id = aws_vpc.main.id
  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.igw.id
  }
}

resource "aws_route_table_association" "a" {
  subnet_id      = aws_subnet.public_a.id
  route_table_id = aws_route_table.public.id
}

resource "aws_route_table_association" "b" {
  subnet_id      = aws_subnet.public_b.id
  route_table_id = aws_route_table.public.id
}

# VPC Endpoint for S3 (for better connectivity and reduced costs)
resource "aws_vpc_endpoint" "s3" {
  vpc_id       = aws_vpc.main.id
  service_name = "com.amazonaws.${var.aws_region}.s3"
  vpc_endpoint_type = "Gateway"
  route_table_ids = [aws_route_table.public.id]
}

resource "aws_security_group" "alb_sg" {
  name        = "${local.name}-alb-sg"
  description = "ALB SG"
  vpc_id      = aws_vpc.main.id
  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "aws_security_group" "ecs_sg" {
  name        = "${local.name}-ecs-sg"
  description = "ECS tasks"
  vpc_id      = aws_vpc.main.id
  ingress {
    from_port       = 0
    to_port         = 65535
    protocol        = "tcp"
    security_groups = [aws_security_group.alb_sg.id]
  }
  egress  {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "aws_ecr_repository" "frontend"   { name = "${local.name}-frontend" }
resource "aws_ecr_repository" "image"      { name = "${local.name}-image" }
resource "aws_ecr_repository" "auth"       { name = "${local.name}-auth" }
resource "aws_ecr_repository" "colab"      { name = "${local.name}-colab" }
resource "aws_ecr_repository" "annotation" { name = "${local.name}-annotation" }

resource "aws_ecs_cluster" "this" {
  name = "${local.name}-cluster"
}

resource "aws_iam_role" "task_exec" {
  name               = "${local.name}-task-exec"
  assume_role_policy = data.aws_iam_policy_document.ecs_assume.json
}

data "aws_iam_policy_document" "ecs_assume" {
  statement {
    actions = ["sts:AssumeRole"]
    principals {
      type        = "Service"
      identifiers = ["ecs-tasks.amazonaws.com"]
    }
  }
}

resource "aws_iam_role_policy_attachment" "exec_logs" {
  role       = aws_iam_role.task_exec.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}

resource "aws_iam_role" "task_role" {
  name               = "${local.name}-task-role"
  assume_role_policy = data.aws_iam_policy_document.ecs_assume.json
}

resource "aws_iam_policy" "bedrock" {
  name   = "${local.name}-bedrock-policy"
  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      { Effect = "Allow", Action = ["bedrock:InvokeModel", "bedrock:InvokeModelWithResponseStream"], Resource = "*" },
      { Effect = "Allow", Action = ["logs:CreateLogGroup", "logs:CreateLogStream", "logs:PutLogEvents"], Resource = "*" }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "task_bedrock" {
  role       = aws_iam_role.task_role.name
  policy_arn = aws_iam_policy.bedrock.arn
}

resource "aws_lb" "alb" {
  name               = "${local.name}-alb"
  internal           = false
  load_balancer_type = "application"
  security_groups    = [aws_security_group.alb_sg.id]
  subnets            = [aws_subnet.public_a.id, aws_subnet.public_b.id]
}

resource "aws_lb_target_group" "tg_front" {
  name        = "${local.name}-front-tg"
  port        = 3000
  protocol    = "HTTP"
  vpc_id      = aws_vpc.main.id
  target_type = "ip"
  health_check {
    path                = "/"
    matcher             = "200-399"
    interval            = 30
    timeout             = 5
    healthy_threshold   = 3
    unhealthy_threshold = 3
  }
}

resource "aws_lb_target_group" "tg_back" {
  name        = "${local.name}-back-tg"
  port        = 8002
  protocol    = "HTTP"
  vpc_id      = aws_vpc.main.id
  target_type = "ip"
  health_check {
    path                = "/api/v1/images/health"
    matcher             = "200-399"
    interval            = 30
    timeout             = 5
    healthy_threshold   = 3
    unhealthy_threshold = 3
  }
}

resource "aws_lb_listener" "http" {
  load_balancer_arn = aws_lb.alb.arn
  port              = 80
  protocol          = "HTTP"
  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.tg_front.arn
  }
}

resource "aws_lb_listener_rule" "api_route" {
  listener_arn = aws_lb_listener.http.arn
  priority     = 10
  action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.tg_back.arn
  }
  condition {
    path_pattern {
      values = ["/api/*"]
    }
  }
}

# Logs
resource "aws_cloudwatch_log_group" "ecs" {
  name              = "/ecs/${local.name}"
  retention_in_days = 7
}

# Task definitions (simplificado: uno frontend, uno backend multi-contenedor)
resource "aws_ecs_task_definition" "backend" {
  family                   = "${local.name}-backend"
  cpu                      = "1024"
  memory                   = "2048"
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  execution_role_arn       = aws_iam_role.task_exec.arn
  task_role_arn            = aws_iam_role.task_role.arn
  runtime_platform {
    operating_system_family = "LINUX"
    cpu_architecture        = "X86_64"
  }

  container_definitions = jsonencode([
    {
      name      = "image-service"
      image     = aws_ecr_repository.image.repository_url
      essential = true
      portMappings = [{ containerPort = 8002, hostPort = 8002 }]
      environment = [
        { name = "MONGODB_URL", value = var.mongo_url },
        { name = "DATABASE_NAME", value = "brainlens" },
        { name = "VLM_PROVIDER", value = "bedrock" },
        { name = "BEDROCK_MODEL_ID", value = var.bedrock_model_id },
        { name = "AWS_REGION", value = var.aws_region },
        { name = "AWS_DEFAULT_REGION", value = var.aws_region },
        { name = "VLM_TIMEOUT", value = tostring(var.vlm_timeout) },
        { name = "COLAB_SERVICE_URL", value = "http://${aws_lb.alb.dns_name}/api/v1/colab" },
        { name = "ENVIRONMENT", value = "production" },
        { name = "ALB_DNS_NAME", value = aws_lb.alb.dns_name },
        { name = "DEBUG", value = "false" },
      ]
      logConfiguration = {
        logDriver = "awslogs"
        options = {
          awslogs-group         = aws_cloudwatch_log_group.ecs.name
          awslogs-region        = var.aws_region
          awslogs-stream-prefix = "image"
        }
      }
    }
  ])
}

resource "aws_ecs_task_definition" "frontend" {
  family                   = "${local.name}-frontend"
  cpu                      = "256"
  memory                   = "512"
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  execution_role_arn       = aws_iam_role.task_exec.arn
  task_role_arn            = aws_iam_role.task_role.arn
  runtime_platform {
    operating_system_family = "LINUX"
    cpu_architecture        = "X86_64"
  }

  container_definitions = jsonencode([
    {
      name      = "frontend"
      image     = aws_ecr_repository.frontend.repository_url
      essential = true
      portMappings = [{ containerPort = 3000, hostPort = 3000 }]
      environment = [
        { name = "REACT_APP_ALB_DNS", value = aws_lb.alb.dns_name },
        { name = "NODE_ENV", value = "production" }
      ]
      logConfiguration = {
        logDriver = "awslogs"
        options = {
          awslogs-group         = aws_cloudwatch_log_group.ecs.name
          awslogs-region        = var.aws_region
          awslogs-stream-prefix = "front"
        }
      }
    }
  ])
}

resource "aws_ecs_service" "back" {
  name            = "${local.name}-back-svc"
  cluster         = aws_ecs_cluster.this.id
  task_definition = aws_ecs_task_definition.backend.arn
  desired_count   = 1
  launch_type     = "FARGATE"
  enable_execute_command = false
  network_configuration {
    subnets         = [aws_subnet.public_a.id, aws_subnet.public_b.id]
    security_groups = [aws_security_group.ecs_sg.id]
    assign_public_ip = true
  }
  load_balancer {
    target_group_arn = aws_lb_target_group.tg_back.arn
    container_name   = "image-service"
    container_port   = 8002
  }
}

resource "aws_ecs_service" "front" {
  name            = "${local.name}-front-svc"
  cluster         = aws_ecs_cluster.this.id
  task_definition = aws_ecs_task_definition.frontend.arn
  desired_count   = 1
  launch_type     = "FARGATE"
  enable_execute_command = false
  network_configuration {
    subnets         = [aws_subnet.public_a.id, aws_subnet.public_b.id]
    security_groups = [aws_security_group.ecs_sg.id]
    assign_public_ip = true
  }
  load_balancer {
    target_group_arn = aws_lb_target_group.tg_front.arn
    container_name   = "frontend"
    container_port   = 3000
  }
}

# Target groups for other backend services
resource "aws_lb_target_group" "tg_auth" {
  name        = "${local.name}-auth-tg"
  port        = 8001
  protocol    = "HTTP"
  vpc_id      = aws_vpc.main.id
  target_type = "ip"
  health_check {
    path                = "/api/v1/auth/health"
    matcher             = "200-399"
    interval            = 30
    timeout             = 5
    healthy_threshold   = 3
    unhealthy_threshold = 3
  }
}

resource "aws_lb_target_group" "tg_annotation" {
  name        = "${local.name}-ann-tg"
  port        = 8003
  protocol    = "HTTP"
  vpc_id      = aws_vpc.main.id
  target_type = "ip"
  health_check {
    path    = "/api/v1/annotations/health"
    matcher = "200-399"
  }
}

resource "aws_lb_target_group" "tg_colab" {
  name        = "${local.name}-colab-tg"
  port        = 8004
  protocol    = "HTTP"
  vpc_id      = aws_vpc.main.id
  target_type = "ip"
  health_check {
    path                = "/health"
    matcher             = "200-399"
    interval            = 30
    timeout             = 5
    healthy_threshold   = 3
    unhealthy_threshold = 3
  }
}

# Listener rules for path routing
resource "aws_lb_listener_rule" "auth_route" {
  listener_arn = aws_lb_listener.http.arn
  priority     = 20
  action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.tg_auth.arn
  }
  condition {
    path_pattern { values = ["/api/v1/auth/*"] }
  }
}

resource "aws_lb_listener_rule" "annotation_route" {
  listener_arn = aws_lb_listener.http.arn
  priority     = 30
  action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.tg_annotation.arn
  }
  condition {
    path_pattern { values = ["/api/v1/annotations/*"] }
  }
}

resource "aws_lb_listener_rule" "colab_route" {
  listener_arn = aws_lb_listener.http.arn
  priority     = 40
  action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.tg_colab.arn
  }
  condition {
    path_pattern { values = ["/api/v1/colab/*"] }
  }
}

# Task definitions for auth, annotation, colab
resource "aws_ecs_task_definition" "auth" {
  family                   = "${local.name}-auth"
  cpu                      = "256"
  memory                   = "512"
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  execution_role_arn       = aws_iam_role.task_exec.arn
  task_role_arn            = aws_iam_role.task_role.arn
  runtime_platform {
    operating_system_family = "LINUX"
    cpu_architecture        = "X86_64"
  }
  container_definitions = jsonencode([
    {
      name      = "auth-service"
      image     = aws_ecr_repository.auth.repository_url
      essential = true
      portMappings = [{ containerPort = 8001, hostPort = 8001 }]
      environment = [
        { name = "MONGODB_URL", value = var.mongo_url },
        { name = "DATABASE_NAME", value = "brainlens" },
        { name = "HOST", value = "0.0.0.0" },
        { name = "PORT", value = "8001" },
        { name = "ENVIRONMENT", value = "production" },
        { name = "ALB_DNS_NAME", value = aws_lb.alb.dns_name },
        { name = "DEBUG", value = "false" },
      ]
      logConfiguration = {
        logDriver = "awslogs"
        options = {
          awslogs-group         = aws_cloudwatch_log_group.ecs.name
          awslogs-region        = var.aws_region
          awslogs-stream-prefix = "auth"
        }
      }
    }
  ])
}

resource "aws_ecs_task_definition" "annotation" {
  family                   = "${local.name}-annotation"
  cpu                      = "256"
  memory                   = "512"
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  execution_role_arn       = aws_iam_role.task_exec.arn
  task_role_arn            = aws_iam_role.task_role.arn
  runtime_platform {
    operating_system_family = "LINUX"
    cpu_architecture        = "X86_64"
  }
  container_definitions = jsonencode([
    {
      name      = "annotation-service"
      image     = aws_ecr_repository.annotation.repository_url
      essential = true
      portMappings = [{ containerPort = 8003, hostPort = 8003 }]
      environment = [
        { name = "MONGODB_URL", value = var.mongo_url },
        { name = "DATABASE_NAME", value = "brainlens" },
        { name = "HOST", value = "0.0.0.0" },
        { name = "PORT", value = "8003" },
        { name = "ENVIRONMENT", value = "production" },
        { name = "ALB_DNS_NAME", value = aws_lb.alb.dns_name },
        { name = "DEBUG", value = "false" },
      ]
      logConfiguration = {
        logDriver = "awslogs"
        options = {
          awslogs-group         = aws_cloudwatch_log_group.ecs.name
          awslogs-region        = var.aws_region
          awslogs-stream-prefix = "annotation"
        }
      }
    }
  ])
}

resource "aws_ecs_task_definition" "colab" {
  family                   = "${local.name}-colab"
  cpu                      = "512"
  memory                   = "1024"
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  execution_role_arn       = aws_iam_role.task_exec.arn
  task_role_arn            = aws_iam_role.task_role.arn
  runtime_platform {
    operating_system_family = "LINUX"
    cpu_architecture        = "X86_64"
  }
  container_definitions = jsonencode([
    {
      name      = "colab-service"
      image     = aws_ecr_repository.colab.repository_url
      essential = true
      portMappings = [{ containerPort = 8004, hostPort = 8004 }]
      environment = [
        { name = "MONGODB_URL", value = var.mongo_url },
        { name = "DATABASE_NAME", value = "brainlens" },
        { name = "HOST", value = "0.0.0.0" },
        { name = "PORT", value = "8004" },
        { name = "ENVIRONMENT", value = "production" },
        { name = "ALB_DNS_NAME", value = aws_lb.alb.dns_name },
        { name = "DEBUG", value = "false" },
      ]
      logConfiguration = {
        logDriver = "awslogs"
        options = {
          awslogs-group         = aws_cloudwatch_log_group.ecs.name
          awslogs-region        = var.aws_region
          awslogs-stream-prefix = "colab"
        }
      }
    }
  ])
}

# Services for auth, annotation, colab
resource "aws_ecs_service" "auth" {
  name            = "${local.name}-auth-svc"
  cluster         = aws_ecs_cluster.this.id
  task_definition = aws_ecs_task_definition.auth.arn
  desired_count   = 1
  launch_type     = "FARGATE"
  network_configuration {
    subnets         = [aws_subnet.public_a.id, aws_subnet.public_b.id]
    security_groups = [aws_security_group.ecs_sg.id]
    assign_public_ip = true
  }
  load_balancer {
    target_group_arn = aws_lb_target_group.tg_auth.arn
    container_name   = "auth-service"
    container_port   = 8001
  }
}

resource "aws_ecs_service" "annotation" {
  name            = "${local.name}-ann-svc"
  cluster         = aws_ecs_cluster.this.id
  task_definition = aws_ecs_task_definition.annotation.arn
  desired_count   = 1
  launch_type     = "FARGATE"
  network_configuration {
    subnets         = [aws_subnet.public_a.id, aws_subnet.public_b.id]
    security_groups = [aws_security_group.ecs_sg.id]
    assign_public_ip = true
  }
  load_balancer {
    target_group_arn = aws_lb_target_group.tg_annotation.arn
    container_name   = "annotation-service"
    container_port   = 8003
  }
}

resource "aws_ecs_service" "colab" {
  name            = "${local.name}-colab-svc"
  cluster         = aws_ecs_cluster.this.id
  task_definition = aws_ecs_task_definition.colab.arn
  desired_count   = 1
  launch_type     = "FARGATE"
  network_configuration {
    subnets         = [aws_subnet.public_a.id, aws_subnet.public_b.id]
    security_groups = [aws_security_group.ecs_sg.id]
    assign_public_ip = true
  }
  load_balancer {
    target_group_arn = aws_lb_target_group.tg_colab.arn
    container_name   = "colab-service"
    container_port   = 8004
  }
}


